apply plugin: 'maven'
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = "1.3.0"

android {
    compileSdkVersion 26
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        versionName version
        versionCode Integer.valueOf(version.toString().replace('.', '').replace('-SNAPSHOT', ''))

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// https://developer.android.com/studio/build/maven-publish-plugin
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId 'com.adnuntius.android'
                artifactId 'sdk'

                from components.release

                pom {
                    name = 'Adnuntius Android SDK'
                    packaging = 'aar'
                    description = 'Adnuntius Android SDK is an android sdk which allows business partners to embed Adnuntius ads in their native android applications.'
                    url = 'http://www.example.com/example-application'

                    scm {
                        connection = 'scm:svn:https://github.com/Adnuntius/android_sdk.git'
                        developerConnection = 'scm:svn:https://github.com/Adnuntius/android_sdk.git'
                        url = 'https://github.com/Adnuntius/android_sdk'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    organization {
                        name = 'Adnuntius AS'
                        url = 'https://adnuntius.com'
                    }

                    developers {
                        developer {
                            id = 'adnuntius'
                            name = 'Adnuntius'
                            email = 'ops@adnuntius.com'
                        }
                    }
                }

                artifact(sourceJar) {
                    classifier = 'sources'
                }
                artifact(javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"

                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

dependencies {
    implementation 'com.android.volley:volley:1.2.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    testImplementation 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set("javadoc")
}

artifacts {
    archives sourceJar
    archives javadocJar
}

afterEvaluate {
    signing {
        sign publishing.publications.release
    }
}
